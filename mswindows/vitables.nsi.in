#       Copyright (C) 2005-2007 Carabos Coop. V. All rights reserved
#       Copyright (C) 2008-2011 Vicent Mas. All rights reserved
#       Author:  Vicent Mas - vmas@vitables.org
#
# This script is intended to generate a binary installer for sources imported
# from a ViTables repository. It is not intended for distribution.

# Use Modern User Interface style
!include MUI.nsh

!include Library.nsh
!include Sections.nsh
!include LogicLib.nsh
#!include WordFunc.nsh
#!insertmacro VersionCompare
#!insertmacro VersionConvert
#!insertmacro WordReplace

# Select the compressor that will compress the installer
SetCompressor /SOLID lzma

# The name of the installer
Name "ViTables @VERSION@"

# The file where the installer will be saved
OutFile "ViTables-@VERSION@.win32-py@DOTPYTHONVERSION@.exe"

# To ensure that shortcuts are removed from the Start Menu on Windows Vista
RequestExecutionLevel admin

# The installer icons as used by MUI (see NSIS FAQ)
!define MUI_ICON "@ROOT@\vitables\icons\vtinstaller.ico"
!define MUI_UNICON "@ROOT@\vitables\icons\vtinstaller.ico"

# Use the new XP style
XPStyle on

# Variables declaration
Var VITABLES_KEY
Var VITABLES_SUBKEY
Var HDF5FILE_SUBKEY
Var HDF5SHELL_SUBKEY
Var STARTMENU_FOLDER
Var MUI_TEMP
Var PYTHONDIR

###########################################################
#	PART I: INSTALLER
###########################################################

# --------------------------------
# Functions
# --------------------------------
Function .onInit
	# The registry key where application settings will be stored
	StrCpy $VITABLES_KEY "SOFTWARE\ViTables\@VERSION@"
	StrCpy $VITABLES_SUBKEY "$VITABLES_KEY\Python@PYTHONVERSION@"
	StrCpy $STARTMENU_FOLDER "ViTables @VERSION@ for Python @DOTPYTHONVERSION@"

	# Check the python installation
	ExecWait "pythonw -c $\"import sys; (sys.version_info[0:3] > (2, 6)) or 0/0$\"" $R0
	${If} $R0 == "1"
	  MessageBox MB_OK|MB_ICONEXCLAMATION "It seems that Python 2.6 is not \
	  installed. Aborting the ViTables installation process."
	  Abort
	${EndIf}

	# Check the tables installation
	ExecWait "pythonw -c $\"import tables$\""
	IfErrors 0 +3
	  MessageBox MB_OK|MB_ICONEXCLAMATION "It seems that PyTables is not \
	  installed. Aborting the ViTables installation process."
	  Abort

	# Check the PyQt4.QtGui installation
	ExecWait "pythonw -c $\"from PyQt4.QtGui import *$\""
	IfErrors 0 +3
	  MessageBox MB_OK|MB_ICONEXCLAMATION "It seems that PyQt4.QtGui is not \
	  installed. Aborting the ViTables installation process."
	  Abort

	# Check the PyQt4.QtCore installation
	ExecWait "pythonw -c $\"from PyQt4.QtCore import *$\""
	IfErrors 0 +3
	  MessageBox MB_OK|MB_ICONEXCLAMATION "It seems that PyQt4.QtCore is not \
	  installed. Aborting the ViTables installation process."
	  Abort
FunctionEnd

Function preInstallDir
  # Default installation directory
  StrCpy $INSTDIR "$PROGRAMFILES\ViTables\$STARTMENU_FOLDER"
FunctionEnd

Function prePythonDir
  # The registry key where python install path is stored
  ReadRegStr $PYTHONDIR HKLM "SOFTWARE\Python\PythonCore\@DOTPYTHONVERSION@\InstallPath" ""
  ClearErrors

FunctionEnd

Function postPythonDir
  IfFileExists $PYTHONDIR\pythonw.exe +3 0
  MessageBox MB_YESNO "Cannot find a valid Python@DOTPYTHONVERSION@ interpreter in \
$PYTHONDIR.$\r$\nYou can install ViTables there, but it will not run \
without Python@DOTPYTHONVERSION@ installed in the same directory tree.$\r$\nContinue \
anyway?" IDYES +2 IDNO 0
  Abort
FunctionEnd

# --------------------------------
# Pages
# --------------------------------

# Page: Welcome
!insertmacro MUI_PAGE_WELCOME

# Page: License
!insertmacro MUI_PAGE_LICENSE @ROOT@\@LICENSEFILENAME@

# Page: Components selection
#!insertmacro MUI_PAGE_COMPONENTS

# Page: Installation directory for ViTables data files
!define MUI_PAGE_CUSTOMFUNCTION_PRE preInstallDir
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will install ViTables @VERSION@ data \
files in the following folder. To install in a different folder, click \
Browse and select another folder. Click Next to Continue."
!insertmacro MUI_PAGE_DIRECTORY

# Page: Installation directory for ViTables modules and scripts
!define MUI_PAGE_CUSTOMFUNCTION_PRE prePythonDir
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE postPythonDir
!define MUI_PAGE_HEADER_TEXT "Choose the Python-@DOTPYTHONVERSION@ interpreter"
!define MUI_PAGE_HEADER_SUBTEXT "Choose the folder in which the Python-@DOTPYTHONVERSION@ \
interpreter is located."
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will install ViTables @VERSION@ modules and \
scripts under the following folder. To install in a different folder, click \
Browse and select another folder. Click Next to Continue."
!define MUI_DIRECTORYPAGE_VARIABLE $PYTHONDIR
!insertmacro MUI_PAGE_DIRECTORY


# Page: Start Menu Folder Configuration
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKLM"
!define MUI_STARTMENUPAGE_REGISTRY_KEY $VITABLES_SUBKEY
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
!insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

# Page: Install selected components
!insertmacro MUI_PAGE_INSTFILES

# ------------------------------------------
# The stuff to install a.k.a. Sections
# ------------------------------------------

Section "ViTables @VERSION@" VT

  # Copy the ViTables script in the Python Scripts directory
  SetOutPath $PYTHONDIR\Scripts
  File vitables.exe
  File vitables.exe.manifest

  # Copy the ViTables sources in the Python site-packages directory
  SetOutPath $PYTHONDIR\Lib\site-packages\vitables
  File /r /x images /x .leo_shadow @ROOT@\vitables\*.py
  File /r /x images @ROOT@\vitables\*.ui
  SetOutPath $PYTHONDIR\Lib\site-packages\vitables\icons
  File /r @ROOT@\vitables\icons\*
  SetOutPath $PYTHONDIR\Lib\site-packages\vitables\htmldocs
  File /r @ROOT@\vitables\htmldocs\*

  # The text files: TODO.txt, INSTALL.txt and stuff like that
  SetOutPath $INSTDIR
  File @ROOT@\*.txt
  File ..\LICENSE.html

  # The documentation directory
  SetOutPath $INSTDIR\doc\html
  File /r @ROOT@\vitables\htmldocs\*
#  SetOutPath $INSTDIR\doc
#  File @ROOT@\doc\ViTablesUsersGuide.pdf

  # The examples directory
  SetOutPath $INSTDIR\examples
  File /r @ROOT@\examples\*.*

  # The translations directory
  SetOutPath $INSTDIR\translations
  File @ROOT@\translations\*.txt
  File @ROOT@\translations\*.qm

  # Create the uninstaller and put it in the aplication directory
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application

    # Create shortcuts
    CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\ViTables.lnk" "$PYTHONDIR\Scripts\vitables.exe"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Examples.lnk" "$INSTDIR\examples"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\User's Guide (PDF).lnk" "$INSTDIR\doc\usersguide.pdf"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\User's Guide (HTML).lnk" "$INSTDIR\doc\html\index.html"
    CreateShortCut "$DESKTOP\ViTables.lnk" "$PYTHONDIR\Scripts\vitables.exe"

  !insertmacro MUI_STARTMENU_WRITE_END

  # Update the registry
  WriteRegStr HKLM $VITABLES_SUBKEY "INSTDIR" $INSTDIR
  WriteRegStr HKLM $VITABLES_SUBKEY "PYTHONDIR" $PYTHONDIR

  # Remove the existing file association (if any) for the .h5 extension
  # TODO we should backup the key and restore it once vitables has been uninstalled
  DeleteRegKey HKCR ".h5"
  # Map the .h5 extension to ViTables
  StrCpy $HDF5FILE_SUBKEY "ViTables\@VERSION@\HDF5File"
  WriteRegStr HKCR ".h5" "" $HDF5FILE_SUBKEY
  # Default settings for the .h5 extension 
  WriteRegStr HKCR $HDF5FILE_SUBKEY "" "HDF5 file"
  WriteRegStr HKCR $HDF5FILE_SUBKEY\DefaultIcon "" "$PYTHONDIR\Lib\site-packages\vitables\icons\vitables_file.ico"
  WriteRegStr HKCR $HDF5FILE_SUBKEY\shell "" ""
  StrCpy $HDF5SHELL_SUBKEY "$HDF5FILE_SUBKEY\shell"
  WriteRegStr HKCR $HDF5SHELL_SUBKEY\open "" "Open with ViTables"
  WriteRegStr HKCR $HDF5SHELL_SUBKEY\open\command "" \
    '$PYTHONDIR\Scripts\vitables.exe "%1"'

SectionEnd

# Set descriptions for Section 0 (VT section)
LangString DESC_VT $(LANG_ENGLISH) "A GUI for PyTables. It allows for very \
fast opening and navigation of arbitrarely large PyTables files."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT 0 $(DESC_VT)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

###########################################################
#	PART II: UNINSTALLER
###########################################################


# --------------------------------
# Pages
# --------------------------------

# Page: Welcome
!insertmacro MUI_UNPAGE_CONFIRM

# Uninstall page: execute uninstall sections
!define MUI_INSTFILESPAGE_FINISHHEADER_TEXT "Uninstallation complete"
!define MUI_INSTFILESPAGE_FINISHHEADER_SUBTEXT "ViTables deletion completed \
succesfully."
!define MUI_UNFINSIHPAGE_NOAUTOCLOSE 1
!insertmacro MUI_UNPAGE_INSTFILES

# Eventually the LANGUAGE macro must be inserted 
!insertmacro MUI_LANGUAGE "English"

# --------------------------------
# Uninstaller Sections
# --------------------------------

Section "Un.Vitables uninstall" UVT

  ReadRegStr $PYTHONDIR HKLM $VITABLES_SUBKEY "PYTHONDIR"
  ReadRegStr $INSTDIR HKLM $VITABLES_SUBKEY "INSTDIR"

  # Remove the application directory
  RMDir /r $INSTDIR

  # Remove the vitables package
  RMDir /r "$PYTHONDIR\Lib\site-packages\vitables"
  Delete "$PYTHONDIR\Scripts\vitables.exe"
  Delete "$PYTHONDIR\Scripts\vitables.exe.manifest"

  # Remove the application entry in the Start Menu
  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP

  Delete "$SMPROGRAMS\$MUI_TEMP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\ViTables.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\Examples.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\User's Guide (PDF).lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\User's Guide (HTML).lnk"
  Delete "$DESKTOP\ViTables.lnk"

  # Delete empty start menu parent diretories
  StrCpy $MUI_TEMP "$SMPROGRAMS\$MUI_TEMP"

  startMenuDeleteLoop:
	ClearErrors
    RMDir $MUI_TEMP
    GetFullPathName $MUI_TEMP "$MUI_TEMP\.."

    IfErrors startMenuDeleteLoopDone

    StrCmp $MUI_TEMP $SMPROGRAMS startMenuDeleteLoopDone startMenuDeleteLoop
  startMenuDeleteLoopDone:

  # Delete the ViTables key from the registry
  DeleteRegKey HKLM $VITABLES_SUBKEY

  # Delete the ViTables configuration from the registry
  DeleteRegKey HKCU $VITABLES_KEY

  # File association
  ReadRegStr $1 HKCR ".h5" ""
  # If .h5 extension not mapped to ViTables then do nothing
  ${If} $1 == "ViTables\@VERSION@\HDF5File"
    DeleteRegKey HKCR ".h5"
    DeleteRegKey HKCR "ViTables"
  ${EndIf}

SectionEnd

# --------------------------------
# Functions
# --------------------------------

Function un.onInit
  # Variables set in installer functions are not available here so we must set
  # them again
	StrCpy $VITABLES_KEY "SOFTWARE\ViTables\@VERSION@"
	StrCpy $VITABLES_SUBKEY "$VITABLES_KEY\Python@PYTHONVERSION@"
FunctionEnd
